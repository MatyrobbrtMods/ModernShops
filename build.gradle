plugins {
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '5.1.+'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'io.github.groovymc.modsdotgroovy' version '1.1.2'
    id 'org.spongepowered.mixin' version '0.7.+'
    id 'eclipse'
}

modsDotGroovy {
    dslVersion = '1.1.3'
    platform 'forge'
}

group = 'com.matyrobbrt.modernshops' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'modernshops'

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)


println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: 'parchment', version: '2022.09.18-1.19.2'

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')
            jvmArg '-Xmx2G'

            property 'forge.logging.console.level', 'debug'

            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            property 'forge.enabledGameTestNamespaces', 'modernshops'

            mods {
                modernshops {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/server')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            property 'forge.enabledGameTestNamespaces', 'modernshops'

            mods {
                modernshops {
                    source sourceSets.main
                }
            }
        }

        // This run config launches GameTestServer and runs all registered gametests, then exits.
        // By default, the server will crash when no gametests are provided.
        // The gametest system is also enabled by default for other run configs under the /test command.
        gameTestServer {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            property 'forge.enabledGameTestNamespaces', 'modernshops'

            mods {
                modernshops {
                    source sourceSets.main
                }
            }
        }

        final dataArgs = ['--mod', 'modernshops', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')]

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            args dataArgs

            mods {
                modernshops {
                    source sourceSets.main
                }
            }
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven { url 'https://maven.k-4u.nl' }
    maven { url 'https://modmaven.dev/' }
    maven { url 'https://cursemaven.com' }
    maven { url 'https://maven.moddinginquisition.org/releases' }
    maven { url 'https://maven.tterrag.com/' }
}


dependencies {
    minecraft 'net.minecraftforge:forge:1.19.2-43.1.32'

    runtimeOnly fg.deobf("mekanism:Mekanism:${mekanism_version}")

    implementation fg.deobf("com.matyrobbrt.simplegui:simplegui:$simplegui_version")

    compileOnly(fg.deobf("mezz.jei:jei-${mc_version}-common-api:${jei_version}"))
    compileOnly(fg.deobf("mezz.jei:jei-${mc_version}-forge-api:${jei_version}"))
    // at runtime, use the full JEI jar for Forge
    runtimeOnly(fg.deobf("mezz.jei:jei-${mc_version}-forge:${jei_version}"))
}

// Example for how to get properties into the manifest for reading at runtime.
jar {
    manifest {
        attributes([
                'Specification-Title'     : 'modernshops',
                'Specification-Vendor'    : 'Matyrobbrt',
                'Specification-Version'   : '1', // We are version 1 of ourselves
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : 'Matyrobbrt',
                'Built-on-Minecraft'      : project.mc_version
        ])
    }
    classifier = 'slim'
}

java.withSourcesJar()

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            artifacts = [tasks.jarJar, sourcesJar]
            artifactId = 'modernshops'
        }
    }
    repositories {
        maven {
            name = 'ModdingInquisitionMavenRepo'
            url = 'https://maven.moddinginquisition.org/releases'
            credentials {
                username = findProperty('inquisitionMavenUser') ?: ''
                password = findProperty('inquisitionMavenPassword') ?: ''
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}